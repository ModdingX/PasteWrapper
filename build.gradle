plugins {
    id 'java-library'
    id 'application'
    id 'maven-publish'
}

group 'org.moddingx'
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains:annotations:24.1.0'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'org.zoomba-lang:spark-core:3.0'
    implementation 'com.google.guava:guava:33.2.1-jre'
    implementation 'com.google.code.gson:gson:2.11.0'

    implementation 'org.slf4j:slf4j-api:2.0.13'
    runtimeOnly 'org.slf4j:slf4j-jdk14:2.0.13'
    
    implementation(platform('org.eclipse.jetty:jetty-bom')) { version { strictly '[11.0.21,12.0.0)' } }
}

application.mainClass = "org.moddingx.pastewrapper.Main"

jar {
    manifest.attributes([
            "Main-Class": application.mainClass.get(),
            "Automatic-Module-Name": "org.moddingx.pastewrapper",
            "Implementation-Version": project.version
    ])
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name 'moddingx'
            url 'https://maven.moddingx.org/release'
            credentials(PasswordCredentials)
        }
    }
}
